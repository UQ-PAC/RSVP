.type VisibilityType <: symbol
.decl Visibility(visibility: VisibilityType)
Visibility("Visibility::Public").
Visibility("Visibility::Protected").
Visibility("Visibility::Private").

.type RoleType <: symbol
.decl Role(role: RoleType)
Role("Role::User").
Role("Role::Admin").

.type AccountType <: symbol
.decl Account(id: AccountType)
Account("Account::Alice").
Account("Account::Bob").
Account("Account::Carl").
Account("Account::Tim").

.type AlbumType <: symbol
.decl Album(album: AlbumType)
Album("Album::AliceSceneries").
Album("Album::AliceVacation").
Album("Album::AliceDocuments").
Album("Album::BobSceneries").
Album("Album::BobVacation").
Album("Album::BobDocuments").
Album("Album::CarlSceneries").
Album("Album::CarlVacation").
Album("Album::CarlDocuments").

.type PhotoType <: symbol
.decl Photo(id: PhotoType)
Photo("Photo::AliceMeadow").
Photo("Photo::AlicePassport").
Photo("Photo::AliceHotel").
Photo("Photo::BobMeadow").
Photo("Photo::BobPassport").
Photo("Photo::BobHotel").

.type ActionType <: symbol
.decl Action(action: symbol)
Action("Action::viewPhoto").
Action("Action::removeAlbum").
Action("Action::removePhoto").
Action("Action::uploadPhoto").
Action("Action::createAlbum").

.decl AccountFriends(account: AccountType, friend: AccountType)
AccountFriends("Account::Alice", "Account::Bob").
AccountFriends("Account::Bob",   "Account::Alice").
AccountFriends("Account::Bob",   "Account::Carl").
AccountFriends("Account::Carl",  "Account::Bob").

.decl AccountRole(account: AccountType, role: RoleType)
AccountRole("Account::Alice", "Role::User").
AccountRole("Account::Bob",   "Role::User").
AccountRole("Account::Carl",  "Role::User").
AccountRole("Account::Tim",   "Role::Admin").

.decl AccountAge(accout: AccountType, age: number)
AccountAge("Account::Alice", 22).
AccountAge("Account::Bob",   25).
AccountAge("Account::Carl",  27).
AccountAge("Account::Tim",   34).

.decl AlbumOwner(album: AlbumType, owner: AccountType)
AlbumOwner("Album::AliceSceneries", "Account::Alice").
AlbumOwner("Album::AliceVacation",  "Account::Alice").
AlbumOwner("Album::AliceDocuments", "Account::Alice").
AlbumOwner("Album::BobSceneries",   "Account::Bob").
AlbumOwner("Album::BobVacation",    "Account::Bob").
AlbumOwner("Album::BobDocuments",   "Account::Bob").
AlbumOwner("Album::CarlSceneries",  "Account::Carl").
AlbumOwner("Album::CarlVacation",   "Account::Carl").
AlbumOwner("Album::CarlDocuments",  "Account::Carl").

.decl AlbumVisibility(album: AlbumType, visibility: VisibilityType)
AlbumVisibility("Album::AliceSceneries", "Visibility::Public").
AlbumVisibility("Album::AliceVacation",  "Visibility::Protected").
AlbumVisibility("Album::AliceDocuments", "Visibility::Private").
AlbumVisibility("Album::BobSceneries",   "Visibility::Public").
AlbumVisibility("Album::BobVacation",    "Visibility::Protected").
AlbumVisibility("Album::BobDocuments",   "Visibility::Private").
AlbumVisibility("Album::CarlSceneries",  "Visibility::Public").
AlbumVisibility("Album::CarlVacation",   "Visibility::Protected").
AlbumVisibility("Album::CarlDocuments",  "Visibility::Private").

.decl PhotoSize(photo: PhotoType, size: number)
PhotoSize("Photo::AliceMeadow", 3).
PhotoSize("Photo::AlicePassport", 4).
PhotoSize("Photo::AliceHotel", 7).
PhotoSize("Photo::BobMeadow", 4).
PhotoSize("Photo::BobPassport", 5).
PhotoSize("Photo::BobHotel", 9).

.decl PhotoInAlbum(photo: PhotoType, album: AlbumType)
PhotoInAlbum("Photo::AliceMeadow",   "Album::AliceSceneries").
PhotoInAlbum("Photo::AlicePassport", "Album::AliceDocuments").
PhotoInAlbum("Photo::AliceHotel",    "Album::AliceVacation").
PhotoInAlbum("Photo::BobMeadow",     "Album::BobSceneries").
PhotoInAlbum("Photo::BobPassport",   "Album::BobDocuments").
PhotoInAlbum("Photo::BobHotel",      "Album::BobVacation").

.type ResourceType = PhotoType | AlbumType | AccountType
.type PrincipalType = AccountType

.decl ActionResource(action: ActionType, resource: ResourceType)
ActionResource("Action::viewPhoto",   resource) :- Photo(resource).
ActionResource("Action::removeAlbum", resource) :- Album(resource).
ActionResource("Action::removePhoto", resource) :- Photo(resource).
ActionResource("Action::uploadPhoto", resource) :- Album(resource).
ActionResource("Action::createAlbum", resource) :- Account(resource).

.decl ActionPrincipal(action: ActionType, resource: PrincipalType)
ActionPrincipal("Action::viewPhoto",   resource) :- Account(resource).
ActionPrincipal("Action::removeAlbum", resource) :- Account(resource).
ActionPrincipal("Action::removePhoto", resource) :- Account(resource).
ActionPrincipal("Action::uploadPhoto", resource) :- Account(resource).
ActionPrincipal("Action::createAlbum", resource) :- Account(resource).

.decl PermitV1(action: ActionType, principal: PrincipalType, resource: ResourceType)
// Anyone can see public photos
PermitV1(action, principal, resource) :-
    action = "Action::viewPhoto",
    Account(principal),
    Photo(resource),
    PhotoInAlbum(resource, album),
    AlbumVisibility(album, "Visibility::Public").

// Admins can see any photo
PermitV1(action, principal, resource) :-
    action = "Action::viewPhoto",
    Account(principal),
    Photo(resource),
    AccountRole(principal, "Role::Admin").

// Users can see their own photos
PermitV1(action, principal, resource) :-
    action = "Action::viewPhoto",
    Account(principal),
    Photo(resource),
    PhotoInAlbum(resource, album),
    AlbumOwner(album, principal).

// Friends can see protected photos of their friends
.decl PermitV2(action: ActionType, principal: PrincipalType, resource: ResourceType)
PermitV2(action, principal, resource) :-
    action = "Action::viewPhoto",
    Account(principal),
    Photo(resource),
    PhotoInAlbum(resource, album),
    AlbumVisibility(album, "Visibility::Protected"),
    AlbumOwner(album, owner),
    AccountFriends(principal, owner).

// Users can upload photos to their albums
.decl PermitU1(action: ActionType, principal: PrincipalType, resource: ResourceType)
PermitU1(action, principal, resource) :-
    action = "Action::uploadPhoto",
    Account(principal),
    Album(resource),
    AlbumOwner(resource, owner),
    owner = principal.

.decl PermitC1(action: ActionType, principal: PrincipalType, resource: ResourceType)
// Users can create albums for their own accounts
PermitC1(action, principal, resource) :-
    action = "Action::createAlbum",
    Account(principal),
    Account(resource),
    resource = principal.

// Admins can create albums for anyone
PermitC1(action, principal, resource) :-
    action = "Action::createAlbum",
    Account(principal),
    Account(resource),
    AccountRole(principal, "Role::Admin").

.decl ForbidC1(action: ActionType, principal: PrincipalType, resource: ResourceType)
// Admins cannot create albums for themselves
ForbidC1(action, principal, resource) :-
    action = "Action::createAlbum",
    Account(principal),
    Account(resource),
    AccountRole(resource, "Role::Admin").

.decl PermitR1(action: ActionType, principal: PrincipalType, resource: ResourceType)
PermitR1(action, principal, resource) :-
    action = "Action::removeAlbum",
    Account(principal),
    Album(resource),
    AccountRole(principal, "Role::Admin").

PermitR1(action, principal, resource) :-
    action = "Action::removeAlbum",
    Account(principal),
    Album(resource),
    AlbumOwner(resource, principal).

.decl Permit(action: ActionType, principal: PrincipalType, resource: ResourceType)
Permit(action, principal, resource) :- PermitV1(action, principal, resource).
Permit(action, principal, resource) :- PermitV2(action, principal, resource).
Permit(action, principal, resource) :- PermitU1(action, principal, resource).
Permit(action, principal, resource) :- PermitC1(action, principal, resource).
Permit(action, principal, resource) :- PermitR1(action, principal, resource).

// Explicitly forbidden by the policy
.decl Forbid(action: ActionType, principal: PrincipalType, resource: ResourceType)
Forbid(action, principal, resource) :-
    ForbidC1(action, principal, resource).

// All possible requests
.decl AllRequests(action: ActionType, principal: PrincipalType, resource: ResourceType)
AllRequests(action, principal, resource) :-
    ActionResource(action, resource), ActionPrincipal(action, principal).

// Requests allowed by the policy
.decl PermittedRequests(action: ActionType, principal: PrincipalType, resource: ResourceType)
PermittedRequests(action, principal, resource) :-
    Permit(action, principal, resource), !Forbid(action, principal, resource).

// Requests forbidden by the policy
.decl ForbiddenRequests(action: ActionType, principal: PrincipalType, resource: ResourceType)
ForbiddenRequests(action, principal, resource) :-
    AllRequests(action, principal, resource), !PermittedRequests(action, principal, resource).

.output AllRequests
.output PermittedRequests
.output ForbiddenRequests
.output PermittedRequests(IO=stdout)

// Security Invariants / Boundaries

// 1. There exists at least one admin in the system
// admin.role == Admin::"Admin"
//      for some admin : Account;
.decl AdminExistInvariant(admin: symbol)
AdminExistInvariant(admin) :-
    Account(admin),
    AccountRole(admin, "Role::Admin").

// 2. Admin accounts should not have albums
// principal.type == Account::"Admin" && !principal.albums.isEmpty()
//    for none of principal : Account;
.decl AdminAlbumsInvariant(admin: symbol)
AdminAlbumsInvariant (user) :-
    Account(user),
    AccountRole(user, "Role::Admin"),
    !(count : { AlbumOwner(_, user) } = 0).

// 3. There should be no photos larger than 10MB
// photo.size > 10
//      for none of photo : Photo;
.decl PhotoSizeInvariant(photo: symbol)
PhotoSizeInvariant(photo) :-
    Photo(photo),
    PhotoSize(photo, size),
    size > 10.

// 4. All admins can see all photos of users
// principal.type == Role::"Admin" && forbiddenRequests(Action::"viewPhoto", principal, resource)
//          for none of principal : Account, resource : Photo;
.decl AdminAccessInvariant(admin: symbol)
AdminAccessInvariant(admin) :-
    Account(admin),
    AccountRole(admin, "Account::Admin"),
    Photo(resource),
    ForbiddenRequests("Action::viewPhoto", admin, resource).