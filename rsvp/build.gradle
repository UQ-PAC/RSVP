plugins {
    id 'java'
    id 'test-report-aggregation'
    id 'jacoco-report-aggregation'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    subprojects.each { sub -> 
        testReportAggregation project(':' + sub.name)
        jacocoAggregation project(':' + sub.name)
    };
}

reporting {
    reports {
        testAggregateTestReport(AggregateTestReport) { 
            testSuiteName = "test"
        }
        testSuiteCodeCoverageReport(JacocoCoverageReport) { 
            testSuiteName = "test"
        }
    }
}

test {
    finalizedBy testAggregateTestReport, testSuiteCodeCoverageReport // report is always generated after tests run
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        // Custom cedar-java fork (https://github.com/rebecca-odonoghue/cedar-java)
        implementation 'com.cedarpolicy:cedar-java:3.1.3-rsvp:uber'
        implementation 'com.google.code.gson:gson:2.13.2'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
        implementation libs.guava

        // Use JUnit Jupiter for testing.
        testImplementation libs.junit.jupiter
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'


    }

    // Apply a specific Java toolchain to ease working on different environments.
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    test {
        // Use JUnit Platform for unit tests.
        useJUnitPlatform()

        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }
}

